#!/usr/bin/zsh
# infoline

# TODO cleanup "exported" variables

# desc => readme
local -a readme

# local _return_value

# color settings
# color settings embed into %{ %}
# uses spectrum from omz
# local cOk cFocus cError cNote cImportant cReset cDefault
autoload -U colors && colors
cOk="%{$fg[green]%}"
cFocus="%{$fg[yellow]%}"
# cFocus="%{$FG[214]%}"
cError="%{$fg[red]%}"
# cError="%{$FG[125]%}"
cNote="%{$fg[blue]%}"
cImportant="%{$fg[magenta]%}"
cReset="%{$reset_color%}"
cDefault="%{$fg[default]%}"

info_user() {
  local face="â˜»"
  if [ $UID -eq 0 ]; then
    echo -n "${cFocus}%n${face}"
  else
    echo -n "%n${face}"
  fi
}

## * Show host if in remote (ssh) computer
info_host() {
  local face="âŒ˜"
  if [ -n "$SSH_CLIENT" ]; then
    print -n "${cFocus}${face}%m"
  fi
}

## * Program return code if error
info_rc() {
  local face_ok face_err
  face_ok="â˜‘"
  face_ok="ðŸ—¹"
  face_err="â˜’"
  face_err="âš "
  face_err="ðŸ—·"
  if [ $_return_value -ne 0 ]; then
    print -n $cError$_return_value$face_err
  fi
  unset _return_value
}

info_history() {
  local face="h"
  face="â›¬"
  echo -n "%h${face}"
}

## * Current Working directory
##   * show cwd, trunkated from start if not enough room
##   * last path part as focus if writable
##   * last path part as error if not writable'
info_cwd() {
  local path color maxwidth
  maxwidth=$(( 1 - 5 ))
  [[ -w $PWD ]] && color=$cNote || color=$cError
  path="%~"
  path=${(%)path}
  path="%$maxwidth<...<$path%<<"
  path=${(%)path}
  if [ $path = "~" ] || [ $path = "/" ]; then
    print -n ${color}${path}
    return
  fi
  print -n ${path:h}
  [[ ${path:h} != "/" ]] && print -n /
  print -n ${color}${path:t}
}

ZSH_THEME_GIT_PROMPT_UNTRACKED="${cImportant}?"
ZSH_THEME_GIT_PROMPT_ADDED="${cFocus}âž•"
ZSH_THEME_GIT_PROMPT_MODIFIED="${cImportant}ðŸŸ‰"
ZSH_THEME_GIT_PROMPT_RENAMED="${cNote}~"
ZSH_THEME_GIT_PROMPT_DELETED="${cNote}âž–"
ZSH_THEME_GIT_PROMPT_STASHED="${cFocus}#"
ZSH_THEME_GIT_PROMPT_UNMERGED="${cNote}&"
ZSH_THEME_GIT_PROMPT_AHEAD="${cNote}â«"
ZSH_THEME_GIT_PROMPT_BEHIND="${cNote}â¬"
ZSH_THEME_GIT_PROMPT_DIVERGED="${cImportant}!"

## * Git status
info_git() {
  local branch
  branch=$(git_current_branch)
  print -n $(git_prompt_status)${branch}
}

## * Number of spawned jobs from shell
info_jobs() {
  local njobs
  njobs=$(jobs -l | wc -l)
  [[ $njobs -gt 0 ]] && print -n "${cNote}${njobs}âš™"
}

info_stub() {
  print -n $cOk"12345"
}

info_empty() {
  print -n ""
}

## * Colored start arrow if root
info_start() {
  if [ $UID -eq 0 ]; then
    print -n $cImportant"%n â–¶ "
  else
    print -n $cFocus"â–¶ "
  fi
}

## * Shell level
info_level() {
  if [ $SHLVL -gt 1 ]; then
    print -n "$cFocusâ®‡"
  fi
}

info_time() {
  print -n "%*"
}

# local clock
typeset -AHg clock

clock=(
  0000 ðŸ•› 0030 ðŸ•§ 0100 ðŸ• 0130 ðŸ•œ 0200 ðŸ•‘ 0230 ðŸ•
  0300 ðŸ•’ 0330 ðŸ•ž 0400 ðŸ•“ 0430 ðŸ•Ÿ 0500 ðŸ•” 0530 ðŸ• 
  0600 ðŸ•• 0630 ðŸ•¡ 0700 ðŸ•– 0730 ðŸ•¢ 0800 ðŸ•— 0830 ðŸ•£
  0900 ðŸ•˜ 0930 ðŸ•¤ 1000 ðŸ•™ 1030 ðŸ•¥ 1100 ðŸ•š 1130 ðŸ•¦
)

## * Analog clock (within 30m)
info_clock() {
  local hours minutes
  hours=$(date +%I)
  minutes=$(date +%M)
  if [ $minutes -lt 15 ]; then
    minutes=00
  elif [ $minutes -lt 45 ]; then
    minutes=30
  else
    hours=$(( hours + 1 ))
    [[ hours -ge 12 ]] && hours=00
    minutes=00
  fi
  print -n $clock[$(printf "%02d%02d" $hours $minutes)]
}

## * Disk usage alert if over 80% capacity
info_disk() {
  local use
  use=${(S)$(df --output=pcent .)//[^0-9]}
  use=${(S)use// } # use have strange space, kill it
  if [ $use -gt 80 ]; then
    print -n "${cFocus}${use}ðŸ–¸"
  fi
}

## * Number of todo items in files
info_todos() {
  [[ ! -w $PWD ]] && return
  local value
  value=$(grep  --directories=skip 'TODO ' * | wc -l)
  if [ $value -gt 0 ]; then
    print -n "${cFocus}${value}ðŸ”¨"
  fi
}

infoline_parts=(info_cwd info_git info_jobs info_rc info_host info_level  info_todos info_disk info_clock)

infoline_prompt=(info_start)

render_prompt() {
  _return_value=$? # have to be first, before any command

  local -a parts_info parts_prompt
  local part value
  local remain=$((COLUMNS-2))

  foreach part ($infoline_parts)
    value=$($part $remain)
    if [ -n "$value" ]; then
      width=${#${(%)${(S)value//\%\{*\%\}}}}
      if [ $width -lt $remain ]; then
        parts_info+=${value}${cDefault}
        remain=$((remain - width - 1))
      fi
    fi
  end
  remain=$((remain + 1))

  foreach part ($infoline_prompt)
    tmp=$($part)
    if [ -n "$tmp" ]; then
      parts_prompt+=$tmp${cDefault}
    fi
  end

  print -n $cReset
  print -n $BG[237]
  print -n " "
  print -n ${(j: :)parts_info}
  print -n ${(l:$remain:: :)}
  print -n " "
  print -n $cReset
  print -n ${(j: :)parts_prompt}
}

PROMPT='$(render_prompt)'
#print ${(%)$(render_prompt)}
